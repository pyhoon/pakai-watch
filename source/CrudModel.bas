B4J=true
Group=Models
ModulesStructureVersion=1
Type=Class
Version=10.5
@EndOfDesignText@
' Crud Model Template
' Customize this template for your database table.
Sub Class_Globals
	Private DB As MiniORM
	
	' Define the database table name
	Private Const TABLE_NAME As String = "tbl_items"
End Sub

Public Sub Initialize
	DB = Main.DB
End Sub

' Retrieve a single row by ID
Public Sub GetRowById (Id As Int) As Map
	DB.Open
	DB.Table = TABLE_NAME
	DB.Columns = Array("id", "item_code", "item_name", "item_price", "item_description")
	DB.Condition = "id = ?"
	DB.Parameter = Id
	DB.Query
	If DB.Found Then
		Return DB.First
	End If
	Return CreateMap()
End Sub

' Check if a row exists by ID
Public Sub FindRowById (Id As Int) As Boolean
	DB.Open
	DB.Table = TABLE_NAME
	DB.Find(Id)
	Return DB.Found
End Sub

' Check if a row exists by code (useful for unique constraints)
Public Sub FindRowByCode (Code As String) As Boolean
	DB.Open
	DB.Table = TABLE_NAME
	DB.Conditions = Array("item_code = ?")
	DB.Parameters = Array(Code)
	DB.Query
	Return DB.Found
End Sub

' Check if a row exists by code excluding a specific ID
Public Sub FindRowByCodeNotEqualId (Code As String, Id As Int) As Boolean
	DB.Open
	DB.Table = TABLE_NAME
	DB.Conditions = Array("item_code = ?", "id <> ?")
	DB.Parameters = Array(Code, Id)
	DB.Query
	Return DB.Found
End Sub

' Search rows by keyword
Public Sub Search (Keyword As String) As List
	DB.Open
	DB.Table = TABLE_NAME
	DB.Columns = Array("id", "item_code", "item_name", "item_price", "item_description")
	If Keyword <> "" Then
		DB.Conditions = Array("UPPER(item_code) LIKE ? Or UPPER(item_name) LIKE ? Or UPPER(item_description) LIKE ?")
		DB.Parameters = Array("%" & Keyword.ToUpperCase & "%", "%" & Keyword.ToUpperCase & "%", "%" & Keyword.ToUpperCase & "%")
	End If
	DB.OrderBy = CreateMap("id": "DESC")
	DB.Query
	Return DB.Results
End Sub

Public Sub Found As Boolean
	Return DB.Found
End Sub

Public Sub First As Map
	Return DB.First
End Sub

Public Sub Error As Exception
	Return DB.Error
End Sub

' Insert a new row
Public Sub Create (Code As String, Name As String, Price As Double, Description As String)
	DB.Open
	DB.Table = TABLE_NAME
	DB.Columns = Array("item_code", "item_name", "item_price", "item_description")
	DB.Parameters = Array(Code, Name, Price, Description)
	DB.ReturnRow = True
	DB.Save
End Sub

' Read all rows
Public Sub Read As List
	DB.Open
	DB.Table = TABLE_NAME
	DB.Columns = Array("id", "item_code", "item_name", "item_price", "item_description")
	DB.OrderBy = CreateMap("id": "DESC")
	DB.Query
	Return DB.Results
End Sub

' Update an existing row
Public Sub Update (Id As Int, Code As String, Name As String, Price As Double, Description As String)
	DB.Open
	DB.Table = TABLE_NAME
	DB.Columns = Array("item_code", "item_name", "item_price", "item_description")
	DB.Parameters = Array(Code, Name, Price, Description)
	DB.Condition = "id = ?"
	DB.Parameter = Id
	DB.ReturnRow = True
	DB.Save
End Sub

' Delete a row by ID
Public Sub Delete (Id As Int)
	DB.Open
	DB.Table = TABLE_NAME
	DB.Id = Id
	DB.Delete
End Sub

' Database Table setup helper
Public Sub CreateItemsTable
	DB.ShowExtraLogs = True
	DB.UseTimestamps = True
	DB.QueryExecute = False
	DB.QueryAddToBatch = True
	DB.IfNotExist = True
	
	Log("Creating Items table...")
	DB.Open
	DB.Table = TABLE_NAME
	DB.Columns.Add(CreateMap("Name": "item_code", "Length": "12", "Null": False))
	DB.Columns.Add(CreateMap("Name": "item_name", "Null": False))
	DB.Columns.Add(CreateMap("Name": "item_price", "Type": DB.DECIMAL, "Length": "10,2", "Null": False, "Default": "0.00"))
	DB.Columns.Add(CreateMap("Name": "item_description", "Null": True))
	DB.Create
	
	Wait For (DB.ExecuteBatchAsync) Complete (Success As Boolean)
	If Success Then
		Log("Table Items created successfully!")
	Else
		Log("Table Items creation failed!")
	End If
	DB.Close
	DB.QueryExecute = True
End Sub
