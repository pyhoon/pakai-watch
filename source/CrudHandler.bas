B4J=true
Group=Handlers
ModulesStructureVersion=1
Type=Class
Version=10.3
@EndOfDesignText@
' Crud Handler Template
' Handles web routes and HTMX endpoints. Customize the routes and model references.
Sub Class_Globals
	Private App As EndsMeet
	Private Path As String
	Private Method As String
	Private View As CrudView
	Private Model As CrudModel
	Private Request As ServletRequest
	Private Response As ServletResponse
	
	' Customize route endpoints
	Private Const ROUTE_PREFIX As String = "/hx/items"
End Sub

Public Sub Initialize
	App = Main.App
	View.Initialize
	Model.Initialize	
End Sub

Sub Handle (req As ServletRequest, resp As ServletResponse)
	Request = req
	Response = resp
	Path = Request.RequestURI
	Method = Request.Method.ToUpperCase
	Log($"${Method}: ${Path}"$)
	
	' Route URL dispatching
	If Path = "/" Or Path = "/items" Then
		HandlePage
	Else If Path = ROUTE_PREFIX & "/table" Then
		HandleTable
	Else If Path = ROUTE_PREFIX & "/add" Then
		HandleModalAdd
	Else If Path.StartsWith(ROUTE_PREFIX & "/edit/") Then
		HandleModalEdit
	Else If Path.StartsWith(ROUTE_PREFIX & "/delete/") Then
		HandleModalDelete
	Else
		HandleCrudActions
	End If
End Sub

Private Sub HandlePage
	App.WriteHtml2(Response, View.Show, App.ctx)
End Sub

' Returns the list of rows (optionally filtered by keyword search)
Private Sub HandleTable
	Dim keyword As String = Request.GetParameter("keyword")
	Dim Rows As List = Model.Search(keyword)
	App.WriteHtml(Response, View.RenderedTable(Rows))
End Sub

' Show Add Modal
Private Sub HandleModalAdd
	App.WriteHtml(Response, View.Modal("Add", Null))
End Sub

' Show Edit Modal
Private Sub HandleModalEdit
	Try
		Dim id As Int = Path.SubString((ROUTE_PREFIX & "/edit/").Length)
	Catch
		Log(LastException)
		ShowAlert($"Error parsing ID: ${LastException.Message}"$, "danger")
		Return
	End Try
	Dim Data As Map = Model.GetRowById(id)
	If Model.Error.IsInitialized Then
		ShowAlert($"Database error: ${Model.Error.Message}"$, "danger")
		Return
	End If
	App.WriteHtml(Response, View.Modal("Edit", Data))
End Sub

' Show Delete Modal
Private Sub HandleModalDelete
	Try
		Dim id As Int = Path.SubString((ROUTE_PREFIX & "/delete/").Length)
	Catch
		Log(LastException)
		ShowAlert($"Error parsing ID: ${LastException.Message}"$, "danger")
		Return
	End Try
	Dim Data As Map = Model.GetRowById(id)
	If Model.Error.IsInitialized Then
		ShowAlert($"Database error: ${Model.Error.Message}"$, "danger")
		Return
	End If
	App.WriteHtml(Response, View.Modal("Delete", Data))
End Sub

' Handle Database POST, PUT, and DELETE operations
Private Sub HandleCrudActions
	Select Method
		Case "POST"
			' Create
			Dim code As String = Request.GetParameter("code")
			Dim name As String = Request.GetParameter("name")
			Dim tempprice As String = Request.GetParameter("price")
			Dim price As Double = IIf(tempprice.Trim = "", 0, tempprice)
			Dim description As String = Request.GetParameter("description")

			If code = "" Or code.Trim.Length < 2 Then
				ShowAlert("Item Code must be at least 2 characters long.", "warning")
				Return
			End If
			
			' Check code uniqueness
			Dim Found As Boolean = Model.FindRowByCode(code)
			If Model.Error.IsInitialized Then
				ShowAlert($"Database error: ${Model.Error.Message}"$, "danger")
				Return
			End If
			If Found Then
				ShowAlert("Item Code already exists!", "warning")
				Return
			End If

			' Save record
			Model.Create(code, name, price, description)
			If Model.Error.IsInitialized Then
				ShowAlert($"Database error: ${Model.Error.Message}"$, "danger")
				Return
			End If			
			ShowToast("Item", "created", "Item created successfully!", "success")
			
		Case "PUT"
			' Update
			Dim id As Int = Request.GetParameter("id")
			Dim code As String = Request.GetParameter("code")
			Dim name As String = Request.GetParameter("name")
			Dim price As Double = Request.GetParameter("price")
			Dim description As String = Request.GetParameter("description")
			
			If code = "" Or code.Trim.Length < 2 Then
				ShowAlert("Item Code must be at least 2 characters long.", "warning")
				Return
			End If
			If name = "" Or name.Trim.Length < 2 Then
				ShowAlert("Item Name must be at least 2 characters long.", "warning")
				Return
			End If
			
			Dim Found As Boolean = Model.FindRowById(id)
			If Model.Error.IsInitialized Then
				ShowAlert($"Database error: ${Model.Error.Message}"$, "danger")
				Return
			End If
			If Not(Found) Then
				ShowAlert("Item not found!", "warning")
				Return
			End If
			
			Dim CodeConflict As Boolean = Model.FindRowByCodeNotEqualId(code, id)
			If Model.Error.IsInitialized Then
				ShowAlert($"Database error: ${Model.Error.Message}"$, "danger")
				Return
			End If
			If CodeConflict Then
				ShowAlert("Item Code already exists on another item!", "warning")
				Return
			End If
			
			' Update record
			Model.Update(id, code, name, price, description)
			If Model.Error.IsInitialized Then
				ShowAlert($"Database error: ${Model.Error.Message}"$, "danger")
				Return
			End If
			ShowToast("Item", "updated", "Item updated successfully!", "info")
			
		Case "DELETE"
			' Delete
			Dim id As Int = Request.GetParameter("id")
			
			Dim Found As Boolean = Model.FindRowById(id)
			If Model.Error.IsInitialized Then
				ShowAlert($"Database error: ${Model.Error.Message}"$, "danger")
				Return
			End If
			If Not(Found) Then
				ShowAlert("Item not found!", "warning")
				Return
			End If

			' Delete record
			Model.Delete(id)
			If Model.Error.IsInitialized Then
				ShowAlert($"Database error: ${Model.Error.Message}"$, "danger")
				Return
			End If
			ShowToast("Item", "deleted", "Item deleted successfully!", "danger")
	End Select
End Sub

Private Sub ShowAlert (Message As String, Status As String)
	Dim info As AlertInfo = Main.CreateAlertInfo(Message, Status)
	App.WriteHtml(Response, View.Alert(info))
End Sub

Private Sub ShowToast (Entity As String, Action As String, Message As String, Status As String)
	Dim data As List = Model.Read
	Dim info As ToastInfo = Main.CreateToastInfo(Entity, Action, Message, Status)
	App.WriteHtml(Response, View.Toast(info, data))
End Sub
